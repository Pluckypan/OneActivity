apply plugin: 'com.android.library'

android {
    compileSdkVersion COMPILE_SDK.toInteger()

    defaultConfig {
        minSdkVersion MIN_SDK.toInteger()
        targetSdkVersion TARGET_SDK.toInteger()
        versionCode APP_VERCODE.toInteger()
        versionName APP_VERNAME.toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "com.android.support:support-v4:$SUPPORT_V4"
    implementation "com.android.support:appcompat-v7:$APPCOMPAT_V7"
}


//项目提交Maven
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'http://www.1991th.com'                                   // 项目的主页
def gitUrl = 'git@github.com:Pluckypan/OneActivity.git'                 // Git仓库

group = APP_ID.toString()                                               // 类库包名
version = APP_VERNAME.toString()

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name '开局一个Activity,结局仍然一个Activity.Just One Activity in the App.'//项目描述
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'pluckypan'
                        name 'Plucky'
                        email 'pluckypan@126.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection siteUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false //必须添加以免出错
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options{
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet "UTF-8"
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "OneActivity"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}